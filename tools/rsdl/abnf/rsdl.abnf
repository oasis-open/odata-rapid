;----------------------------
; ABNF for RSDL
;----------------------------


;----------------------------
;  Model
;----------------------------

model                = OWS [ namespace RWS ] *include [ modelElement *( RWS modelElement ) ] [ OWS service ] [ OWS paths ] OWS

namespace            = %s"namespace" RWS qualifiedName

include              = %s"include" RWS DQUOTE 1*CHAR DQUOTE RWS %s"as" RWS identifier RWS

modelElement         = ( structuredType / enumType / typeDefinition )


;----------------------------
;  Structured Type
;----------------------------

structuredType       = annotations [ %s"abstract" RWS ] %s"type" RWS identifier [ %s"extends" RWS qualifiedName ] OWS "{" *( OWS structuredTypeMember ) OWS "}"

structuredTypeMember = property / operation ; property, action, or function

property             = singlePropertyDefinition [ OWS (primitivePropertyCapabilities / singleNavigationCapabilities) ]
                     / collectionPropertyDefinition [ OWS ( collectionCapabilities / collectionNavigationCapabilities) ]

singlePropertyDefinition  = annotations [propertyModifier RWS] identifier OWS ":" OWS singleTypeReference

collectionPropertyDefinition  = annotations [propertyModifier RWS] identifier OWS ":" OWS collectionTypeReference

propertyModifier     = %s"key"

singleTypeReference      = typeName [ "?" ]

collectionTypeReference  = "[" typeName [ "?" ] "]"

typeReference            = singleTypeReference / collectionTypeReference

typeName             = builtInType / edmType / qualifiedName

builtInType          = %s"Boolean"
                     / %s"DateTime"
                     / %s"Date"
                     / %s"Decimal" [ "(" precision "," scale ")"]
                     / %s"Double"
                     / %s"Duration"
                     / %s"Integer"
                     / %s"String" [ "(" maxLength ")" ]
                     / %s"TimeOfDay"

edmType              = %s"Edm" "." identifier

operation            = annotations operationKind RWS identifier OWS
                       "(" OWS [ parameter *( OWS "," OWS parameter) OWS ] ")"
                       [ OWS ":" OWS annotations typeReference ]
                       [ separator collectionNavCapabilities ]

operationKind        = %s"action" / %s"function"

parameter            = annotations identifier OWS ":" OWS typeReference


;----------------------------
;  Enumeration Type
;----------------------------

enumType             = annotations ( %s"enum" / %s"flags" ) RWS identifier OWS "{" OWS 1*enumMember "}"

enumMember           = annotations identifier OWS


;----------------------------
;  Type Definition
;----------------------------

typeDefinition       = annotations %s"typedef" RWS identifier OWS ":" OWS ( builtInType / edmType )


;----------------------------
;  Service
;----------------------------

service              = annotations %s"service" [ RWS identifier ] OWS "{" OWS serviceMember *( RWS serviceMember ) OWS "}"

serviceMember        = annotations ( entitySet / singleton / serviceOperation )

entitySet            = identifier OWS ":" OWS "[" qualifiedName "]" [ OWS collectionNavigationCapabilities ]

singleton            = identifier OWS ":" OWS qualifiedName [ OWS singleNavigationCapabilities ]

serviceOperation     = operationKind RWS identifier
                       OWS "(" OWS [ parameter *(OWS "," OWS parameter) OWS ] ")"
                       [ OWS ":" OWS annotations typeReference ]


;----------------------------
;  Annotations
;----------------------------

annotations          = *( annotation RWS )

annotation           = "@" qualifiedName [ "#" identifier ] OWS ":" OWS annotationValue / DOC-COMMENT

annotationValue      = %s"true"
                     / %s"false"
                     / %s"null"
                     / number
                     / DQUOTE *CHAR DQUOTE
                     / "[" OWS [ annotationValue *( separator annotationValue ) OWS [ "," OWS ] ] "]"
                     / "{" OWS [ annotationProperty *( separator annotationProperty ) OWS [ "," OWS ] ] "}"
                     / "." *( "/"  identifier )

annotationProperty   = propertyName OWS ":" OWS annotationValue

propertyName         = identifier / DQUOTE 1*CHAR DQUOTE / "@" qualifiedName [ "#" identifier ]


;----------------------------
;  Model Capabilities
;----------------------------

primitivePropertyCapability = "filterable" [ OWS filterOptions ] / "orderable" [ OWS orderByDirection ]

primitivePropertyCapabilities = "{" OWS [ primitivePropertyCapability *( separator primitivePropertyCapability )] OWS "}"

singleNavigationCapability = ("READ" / "UPDATE" / "REPLACE") [ OWS navCapabilities ] / "DELETE" noOptions

singleNavigationCapabilities = "{" OWS [ singleNavigationCapability *( separator singleNavigationCapability )] OWS "}"

collectionNavigationCapability = "DELETE" OWS noOptions
                              / "LIST" [ OWS collectionNavCapabilities ]
                              / ("READ" / "CREATE" / "REPLACE" / "UPDATE") [ OWS navCapabilities ]

collectionNavigationCapabilities = "{" OWS [ collectionNavigationCapability *( separator collectionNavigationCapability )] OWS "}"



;----------------------------
;  Paths
;----------------------------

paths = %s"paths" OWS "{" *( OWS "/" path ) OWS "}"

path = serviceOperationSegment [ ( RWS serviceOperationCapabilities ) / ( *( "/" interimSegment ) "/" lastSegment ) ]
     / identifier [ "/" castSegment ] [ ( RWS collectionNavPathCapabilities ) / ( "/" keySegment [ ( RWS singleNavPathCapabilities / ( *( "/" interimSegment ) "/" lastSegment ) ) ] ) ]
     / identifier [ "/" castSegment ] [ ( RWS singleNavPathCapabilities ) / ( *( "/" interimSegment ) "/" lastSegment ) ]

interimSegment = collectionNavSegment "/" keySegment
               / serviceOperationSegment
               / castSegment
               / singleValuedSegment
               / singleNavSegment

lastSegment = serviceOperationSegment [ RWS serviceOperationCapabilities ]
            / singleValuedSegment [ "/" castSegment ] [ RWS singlePathCapabilities ]
            / collectionValuedSegment [ "/" castSegment ] [ RWS collectionPathCapabilities ]
            / singleNavSegment [ "/" castSegment ] [ RWS singleNavPathCapabilities ]
            / collectionNavSegment [ "/" castSegment ] [ RWS ( collectionNavPathCapabilities / "/" keySegment RWS singleNavPathCapabilities )]

serviceOperationSegment = identifier parameters [ "/" castSegment ] [ "/" keySegment ]

serviceOperationCapabilities = singlePathCapabilities / collectionPathCapabilities / singleNavPathCapabilities / collectionNavPathCapabilities

singleValuedSegment = identifier                   ; a single property
                    / singleValuedOperation

collectionValuedSegment = identifier               ; a collection-valued property
                    / collectionValuedOperation

singleNavSegment = identifier                      ; a single navigation property
                    / singleNavigationProperty
                    / singleNavValuedOperation

collectionNavSegment = identifier                  ; a collection-valued navigation property
                    / collectionNavigationProperty
                    / collectionNavValuedOperation

singleValuedOperation = identifier                 ; an operation that returns a single value

collectionValuedOperation = identifier             ; an operation that returns a collection value

singleNavValuedOperation = identifier              ; an operation that returns a single navigation value

collectionNavValuedOperation = identifier          ; an operation that returns a collection of navigation values

parameters = "(" OWS parameterSpecification *( "," OWS parameterSpecification ) OWS ")"

parameterSpecification = identifier OWS "=" OWS "{" identifier "}"

castSegment = identifier 1*( "." identifier )

keySegment = "{" keyProperty "}"

keyProperty = identifier                           ; name of the key property

singleNavigationProperty = identifier              ; a single valued navigation property

collectionNavigationProperty = identifier          ; a collection valued navigation property



;----------------------------
;  Path Capabilities
;----------------------------


singlePathCapability = ("GET" / "PUT" / "PATCH" / "DELETE") [noOptions]

singlePathCapabilities = "{" [singlePathCapability *( separator singlePathCapability)] "}"

collectionPathCapability = "GET" [ collectionCapabilities ] / "POST" [noOptions]

collectionPathCapabilities = "{" [ collectionPathCapability *( separator collectionPathCapability )] "}"

singleNavPathCapability = ("GET" / "PATCH" / "PUT") [ OWS navCapabilities ] / "DELETE" noOptions

singleNavPathCapabilities = "{" [singleNavPathCapability *( separator singleNavPathCapability )] "}"

collectionNavPathCapability = "GET" [ OWS collectionNavCapabilities ] / "POST" [ OWS navCapabilities ]

collectionNavPathCapabilities = "{" [ collectionNavPathCapability *( separator collectionNavPathCapability )] "}"



;----------------------------
;  Capability Elements
;----------------------------


collectionCapability = filterCapability / orderByCapability / "top" / "skip" / "count"

collectionCapabilities =  "{" OWS [ collectionCapability *( separator collectionCapability ) OWS ] "}"

collectionNavCapability = collectionCapability / navCapability

collectionNavCapabilities = "{" OWS [ collectionNavCapability *( separator collectionNavCapability ) OWS ] "}"

navCapability       = "expand" [ OWS "(" OWS [ expandProperty *( OWS "," OWS expandProperty OWS ) ] OWS ")" ]

navCapabilities     =  "{" OWS [ navCapability OWS ] "}"

expandProperty      = star /
                      [ castSegment "/" ] collectionNavigationProperty [ OWS collectionNavCapabilities ] /
                      [ castSegment "/" ] ( singleNavigationProperty ) [ OWS navCapabilities ]

filterCapability    = "filter" [ "(" [ OWS filterProperty *( "," OWS filterProperty OWS ) ] ")" ]

filterProperty    = ( ( [ typeName "/" ] propertyName ) / allProperties) [ OWS filterOptions ]

allProperties       =  star [ "/" typeName ]  ; all properties, optionally of a given type

filterOptions        = "{" OWS [ filterOperations OWS ] "}"

filterOperations     =  "none" ; not filterable
                     / "eq" ; eq
                     / "comp" ; eq, gt, ge, lt, le
                     / "stringComp" ; eq, gt, ge, lt, le, startswith, endswith, contains
                     / "string" ; eq, startswith, endswith, contains

orderByCapability = "orderby" [ OWS orderByProperties ]

orderByProperties = "(" OWS [ orderByProperty *( "," OWS orderByProperty OWS ) ] ")"

orderByProperty = allProperties / propertyName [ OWS orderByDirection ]

orderByDirection ="{" [ OWS ascOrDesc [ "," OWS ascOrDesc OWS ] ] "}"

ascOrDesc = "asc" / "desc"

noOptions = OWS "{" OWS "}"



;----------------------------
;  Core Syntax Elements
;----------------------------


qualifiedName       = identifier *( "." identifier )

identifier          = identInitial *identSubsequent

identInitial        = ALPHA / "_" ; Note: actually all Unicode letters

identSubsequent     = identInitial / DIGIT

separator           = OWS "," OWS / RWS

star                = "*"

number              = integer [ "." 1*DIGIT ] [ "e" integer ]

integer             = [ "+" / "-" ] ( %x30 / %x31-39 *DIGIT )

precision           = integer

scale               = integer

maxLength           = integer

DOC-COMMENT         = "##" *( %x0-9 / %xB-C / %xE-10FFFF)

ALPHA               = %x41-5A / %x61-7A

DIGIT               = %x30-39

CHAR                = %x20-21 / %x23-5B / %x5D-10FFFF
                    / ESCAPE ESCAPE
                    / ESCAPE DQUOTE

DQUOTE              = %x22              ; "

ESCAPE              = %x5C              ; \

OWS                 = *WS
RWS                 = 1*WS
WS                  = %x8 / %xA / %xD / %x20 ; TAB, LF, CR, SPACE
